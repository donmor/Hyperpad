apply plugin: 'com.android.application'

def getCommitHash = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'master'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null
    }
}

def properties = new Properties()
def inputStream = project.rootProject.file('local.properties').newDataInputStream()
properties.load(inputStream)

android {
    signingConfigs {
        donmor {
            storeFile file(properties.getProperty('keyFile'))
            storePassword properties.getProperty('signPassword')
            keyAlias = properties.getProperty('keyAlias')
            keyPassword properties.getProperty('signPassword')
        }
    }
    compileSdk 34
    defaultConfig {
        applicationId "top.donmor.hyperpad"
        minSdkVersion 19
        targetSdkVersion 34
        versionCode 6
        versionName "1.1.1"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.internal.runner.junit4.AndroidJUnit4ClassRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.donmor
        }
        debug {
            versionNameSuffix "@" + getCommitHash()
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    namespace 'top.donmor.hyperpad'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'com.ibm.icu:icu4j:65.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
